#use-added-syntax(jitx)
defpackage sharp-sc62015-interposer :
  import core
  import jitx
  import jitx/commands
  import jitx/parts

  import helpers
  import jsl
  import collections

  ; gen-testpad
  import ocdb/utils/generic-components
  ; get-time-string
  import utils/time

val board-shape = RoundedRectangle(
  40.0,
  50.0, 0.0)

pcb-module module :
  ; NOTE: need to use inverted FFC cable to match the pins on the
  ; level-shifter element
  inst ffc : components/FFCConnector/module(flip_pins = false)

  inst cpu : components/SC62015B02/interposer

  port gnd
  port vcc
  net GND (gnd ffc.GND)
  net VCC (vcc ffc.VCC5V)

  net (gnd cpu.GND)
  ; net (vcc cpu.VCC)

  inst tp_gnd : gen-testpad(3.0)
  net (gnd tp_gnd.p)
  inst tp_vcc : gen-testpad(3.0)
  net (vcc tp_vcc.p)

  val data = [

    cpu.D[0],
    cpu.D[1],
    cpu.D[2],
    cpu.D[3],
    cpu.D[4],
    cpu.D[5],
    cpu.D[6],
    cpu.D[7],

    cpu.A[0],
    cpu.A[1],
    cpu.A[2],
    cpu.A[3],
    cpu.A[4],
    cpu.A[5],
    cpu.A[6],
    cpu.A[7],
    cpu.A[8],
    cpu.A[9],
    cpu.A[10],
    cpu.A[11],
    cpu.A[12],
    cpu.A[13],
    cpu.A[14],
    cpu.A[15],
    cpu.A[16],
    cpu.A[17],
    cpu.A[18],

    cpu.DCLK,
    cpu.OUT,

    cpu.CE[7],
    cpu.CE[6],
    cpu.CE[5],
    cpu.CE[4],
    cpu.CE[3],
    cpu.CE[2],
    cpu.CE[1],
    cpu.CE[0],

    cpu.ACLK,
    cpu.DIS,
    cpu.RD,
    cpu.RXD,
    cpu.TXD,

    cpu.RESET,
    cpu.TEST,
    cpu.ON,
    cpu.WR,
    cpu.MRQ,

    cpu.GND,
  ]
  for i in 0 to length(data) do :
    val bus-name = replace(to-string(ref(data[i])), "cpu.", "") as String
    val name = to-string("%_-DATA%_" % [bus-name, i])
    make-net(to-symbol(name), [data[i], ffc.data[i]])

    if bus-name != "GND" :
      var mapping = append("FPGA_MAP: ", to-string(i))
      mapping = append(mapping, " â†’ ")
      mapping = append(mapping, to-string(ref(data[i])))
      println(mapping)

  place(ffc) at loc(0.0, 19.0, 0.0) on Top
  place(cpu) at loc(-4.5, -12.5, 90.0) on Top

  place(tp_gnd) at loc(-17.0, 16.0, 0.0) on Top
  inst tp_gnd_text : ocdb/artwork/board-text/text("GND", 1.5, 0.0)
  place(tp_gnd_text) at loc(-17.0, 13.5, 0.0) on Top

  place(tp_vcc) at loc( 17.0, 16.0, 0.0) on Top
  inst tp_vcc_text : ocdb/artwork/board-text/text("VCC", 1.5, 0.0)
  place(tp_vcc_text) at loc(17.0, 13.5, 0.0) on Top

  val mydate:String = get-time-string("%Y-%m-%d")
  val label-text = append("SC62015 v1 (c) mblsha ", mydate)
  inst version-label : ocdb/artwork/board-text/text(label-text, 1.5, 0.0)
  place(version-label) at loc(0.0, 5.0, 0.0) on Top

; FIXME: use FFC board type in order to have proper clearance specs for JLCPCB
setup-design-flex("sharp-sc62015-interposer", board-shape, signal-shrink = 0.5)
set-main-module(module)
view-schematic()
view-board()
export-to-cad()
