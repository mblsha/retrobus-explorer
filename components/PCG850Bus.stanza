; When renaming the stanza package name below, also rename it in the folder stanza.proj
; See docs.jitx.com for help
#use-added-syntax(jitx)
defpackage components/PCG850Bus:
  import core
  import jitx
  import jitx/commands
  import ocdb/utils/box-symbol
  import jsl
  import ocdb/utils/generic-components
  import ocdb/utils/landpatterns

pcb-landpattern lp :
  val num_pads = 40
  val num_rows = 2
  val num_cols = num_pads / num_rows

  val row_pitch = 2.54 * 2.0
  val col_pitch = 1.27

  val offset_x = (to-double(num_cols) - 1.0) * col_pitch / 2.0 * -1.0
  val offset_y = (to-double(num_rows) - 1.0) * row_pitch / 2.0 + row_pitch / 4.0

  var pin_number = 1
  for col in 0 to num_cols :
    val column_offset = if col % 2 == 1 : row_pitch / 2.0 else : 0.0

    for row in 0 to num_rows :
      val x = to-double(col) * col_pitch
      val y = to-double(row) * row_pitch * -1.0 - column_offset

      if pin_number == 1:
        layer(Silkscreen("F-SilkS", Top)) =
          Circle(Point(x - 1.0 + offset_x, y + 1.0 + offset_y), 0.3)

      pad p[pin_number] : circle-pth-circle-pad(1.0, 1.5, mask = 1.4) at loc(x + offset_x, y + offset_y) on Top
      pin_number = pin_number + 1

  val case_width = 31.50
  val case_height = 8.88
  layer(Silkscreen("F-SilkS", Top)) = Polyline(0.2, [
    Point(case_width / -2.0, case_height / -2.0),
    Point(case_width / -2.0, case_height /  2.0),
    Point(case_width /  2.0, case_height /  2.0),
    Point(case_width /  2.0, case_height / -2.0),
    Point(case_width / -2.0, case_height / -2.0)])

public pcb-component component :
  name = "C5448171"
  description = "1.27mm EDGE SLOT Direct Insert 40P -55℃~+105℃ Plugin,P=1.27mm Edgeboard Connectors ROHS"
  manufacturer = "WingTAT"
  mpn = "PC-G850" ; "HED40LP03BK"
  datasheet = "https://www.lcsc.com/datasheet/lcsc_datasheet_2312301551_WingTAT-HED40LP03BK_C5448171.pdf"
  reference-prefix = "U"

  pin-properties :
    [pin:Ref | pads:Ref ... | side:Dir]
    [VCC     | p[1] p[2]  | Left]
    [M1       | p[3]  | Left]
    [MREQ     | p[4]  | Left]
    [IORQ     | p[5]  | Left]
    [IORESET  | p[6]  | Left]
    [WAIT     | p[7]  | Left]
    [INT1     | p[8]  | Left]
    [WR       | p[9]  | Left]
    [RD       | p[10] | Left]
    [BNK1     | p[11] | Left]
    [BNK0     | p[12] | Left]
    [CEROM2   | p[13] | Left]
    [CERAM2   | p[14] | Left]
    [D7       | p[15] | Left]
    [D6       | p[16] | Left]
    [D5       | p[17] | Left]
    [D4       | p[18] | Left]
    [D3       | p[19] | Left]
    [D2       | p[20] | Left]
    [D1       | p[21] | Right]
    [D0       | p[22] | Right]
    [A15      | p[23] | Right]
    [A14      | p[24] | Right]
    [A13      | p[25] | Right]
    [A12      | p[26] | Right]
    [A11      | p[27] | Right]
    [A10      | p[28] | Right]
    [A9       | p[29] | Right]
    [A8       | p[30] | Right]
    [A7       | p[31] | Right]
    [A6       | p[32] | Right]
    [A5       | p[33] | Right]
    [A4       | p[34] | Right]
    [A3       | p[35] | Right]
    [A2       | p[36] | Right]
    [A1       | p[37] | Right]
    [A0       | p[38] | Right]
    [GND      | p[39] p[40] | Right]

  val box = BoxSymbol(self)
  assign-symbol $ create-symbol(box)

  assign-landpattern(lp)

; view(component)
