; Generated by JITX 3.25.0
#use-added-syntax(jitx)
defpackage sharp-pc-g850 :
  import core
  import jitx
  import jitx/commands
  import jitx/parts

  import helpers
  import jsl
  import collections

  ; gen-testpad
  import ocdb/utils/generic-components
  ; get-time-string
  import utils/time


val board-shape = RoundedRectangle(
  50.0,
  21.0, 3.0)

pcb-module module :
  ; NOTE: need to have flipped pins to match the main board.
  inst ffc : components/FFCConnector/module(flip_pins = true)

  inst bus : components/PCG850Bus/component

  port gnd
  port vcc
  net GND (gnd ffc.GND)
  net VCC (vcc ffc.VCC5V)

  val dx = -0.0
  place(ffc) at loc( 1.5 + dx, 1.0, 0.0) on Top
  place(bus) at loc(-1.0 + dx, -5.0) on Top

  net (gnd bus.GND)
  net (vcc bus.VCC)

  val data = [
    bus.GND ; extra
    bus.MREQ
    bus.M1
    bus.IORESET
    bus.IORQ
    bus.INT1
    bus.WAIT
    bus.RD
    bus.WR
    bus.BNK0
    bus.BNK1
    bus.CERAM2
    bus.CEROM2
    bus.D6
    bus.D7
    bus.D4
    bus.D5
    bus.D2
    bus.D3
    bus.D0
    bus.D1
    bus.A14
    bus.A15
    bus.A12
    bus.A13
    bus.A10
    bus.A11
    bus.A8
    bus.A9
    bus.A6
    bus.A7
    bus.A4
    bus.A5
    bus.A2
    bus.A3
    bus.A0
    bus.A1
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
    bus.GND ; extra
  ]
  for i in 0 to length(data) do :
    val bus-name = replace(to-string(ref(data[i])), "bus.", "") as String
    val name = to-string("%_-DATA%_" % [bus-name, i])
    make-net(to-symbol(name), [data[i], ffc.data[i]])

  ; inst tp0 : gen-testpad(1.0)
  ; net (tp0.p ffc.data[0])
  ; place(tp0) at loc(1.27, 1.27 * 5.0) on Top (relative-to heads[0])

  inst tp-gnd : components/GndTestpads/module(diameter = 3.0, width = width(board-shape), height = height(board-shape))
  net (tp-gnd.GND ffc.GND)
  place(tp-gnd) at loc(0.0, 0.0) on Top

  val mydate:String = get-time-string("%Y-%m-%d")
  val label-text = append("SHARP PC-G850 adapter v4 (c) mblsha ", mydate)
  inst version-label : ocdb/artwork/board-text/text(label-text, 1.5, 0.0)
  val label_y = height(board-shape) / 2.0 - 2.0
  place(version-label) at loc(0.0, label_y, 0.0) on Top

  geom(gnd) :
    copper-pour(LayerIndex(1),         isolate = 0.15, rank = 1) = board-shape
    copper-pour(LayerIndex(2),         isolate = 0.15, rank = 1) = board-shape
    copper-pour(LayerIndex(0, Bottom), isolate = 0.15, rank = 1) = board-shape

setup-design("sharp-pc-g850", board-shape, signal-shrink = 0.5)
set-main-module(module)
view-schematic()
view-board()
export-to-cad()


